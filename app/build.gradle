apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    def DEBUG = readProps('app/config/debug.properties')
    def RELEASE = readProps('app/config/release.properties')

    defaultConfig {
        applicationId 'com.italankin.dictionary'
        minSdkVersion 14
        targetSdkVersion 28
        compileSdkVersion 28
        versionCode 23
        versionName '1.10.1'
        archivesBaseName = "dictionary-$versionName"
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG['keystore'])
            storePassword DEBUG['keystorePasswd']
            keyAlias DEBUG['alias']
            keyPassword DEBUG['aliasPasswd']
        }
        release {
            if (RELEASE != null) {
                storeFile file(RELEASE['keystore'])
                storePassword RELEASE['keystorePasswd']
                keyAlias RELEASE['alias']
                keyPassword RELEASE['aliasPasswd']
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            buildConfigField('String', 'API_KEY', DEBUG['apiKey'])
            buildConfigField('String', 'BASE_URL', DEBUG['baseUrl'])
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release

            if (RELEASE != null) {
                buildConfigField('String', 'API_KEY', RELEASE['apiKey'])
                buildConfigField('String', 'BASE_URL', RELEASE['baseUrl'])
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61'
    implementation 'androidx.core:core-ktx:1.1.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.preference:preference:1.1.0'

    implementation 'com.arello-mobile:moxy:1.5.5'
    kapt 'com.arello-mobile:moxy-compiler:1.5.5'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation 'com.google.dagger:dagger:2.25.4'
    kapt 'com.google.dagger:dagger-compiler:2.25.4'

    implementation 'com.squareup.retrofit2:retrofit:2.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.3.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'
}

/**
 * Construct new properties object and read it from file.
 *
 * @param fileName name of the file
 * @return properties
 */
Properties readProps(String fileName) {
    File file = new File(fileName)
    if (file.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file))
        return props
    }
    return null
}
